apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    delete "$rootDir/docs"

    outputFormat = 'html'
    outputDirectory = "$rootDir/docs"

    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
    }

    // Uncomment when dokka 0.9.16 is out
    // includeNonPublic = false

    // Always recreate documentation
    outputs.upToDateWhen { return false }
}

afterEvaluate {
    if (project.hasProperty("javadocJar")) {
        tasks.javadocJar.dependsOn dokkaJavadoc
    }
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'spannedgridlayoutmanager'

    publishedGroupId = 'com.arasthel'
    libraryName = 'SpannedGridLayoutManager'
    artifact = 'spannedgridlayoutmanager'

    libraryDescription = 'A layout manager that will resize and reorder views based on a provided SpanSize.'

    siteUrl = 'https://github.com/Arasthel/SpannedGridLayoutManager'
    gitUrl = 'https://github.com/Arasthel/SpannedGridLayoutManager.git'

    libraryVersion = '2.0.0'

    developerId = 'Arasthel'
    developerName = 'Jorge Mart√≠n Epsinosa'
    developerEmail = 'jorgemartinespinosa@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
}

dependencies {
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

// Bintray Upload
apply from: '../gradle/tools/bintrayv1.gradle'
apply from: '../gradle/tools/installv1.gradle'